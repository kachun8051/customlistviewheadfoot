Build1=Default,hk.b4a.customlistview.headerfooter
File1=collapsed.png
File10=sectionfooter.png
File11=sectionheader.png
File2=content.json
File3=content2.json
File4=EditHead.bal
File5=EditLine.bal
File6=ExchangeLine.bal
File7=head2.png
File8=InputBox.bal
File9=main.bal
FileGroup1=Default Group
FileGroup10=Default Group
FileGroup11=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
FileGroup6=Default Group
FileGroup7=Default Group
FileGroup8=Default Group
FileGroup9=Default Group
Group=Default Group
Library1=core
Library2=inputmenu
Library3=json
Library4=stringutils
Library5=xcustomlistview
Library6=xui
Library7=xui views
Library8=byteconverter
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: http://www.basic4ppc.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="4" android:targetSdkVersion="14"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~'End of default text.~\n~
Module1=clsMapOfList
Module2=modCommon
NumberOfFiles=11
NumberOfLibraries=8
NumberOfModules=2
Version=11
@EndOfDesignText@
#Region Module Attributes
	#FullScreen: True
	#IncludeTitle: True
	#ApplicationLabel: CLV Header Footer Example
	#VersionCode: 1
	#CanInstallToExternalStorage: False
#End Region

'Activity module
Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'These variables can be accessed from all modules.
	Private xui As XUI
	Private timer As Timer
End Sub

Sub Globals
	Dim clv1 As CustomListView
	Dim Dialog As B4XDialog
	Private objMapOfList As clsMapOfList
	Private edtHead As EditText
	Private edtItem As EditText
	Private edtQty As EditText
	Private sprHead As Spinner
	' OHead record the text before change	
	Private edtOHead As EditText
	' OItem record the item before change
	Private edtOItem As EditText
	' OQty record the qty before change
	Private edtOQty As EditText
	Private lblHead As Label
	' bitmap for header and footer of clv1
	Private bmHeader, bmFooter, bmCollapsed As B4XBitmap
End Sub

Sub Activity_Create(FirstTime As Boolean)
	Activity.LoadLayout("main.bal")
	Activity.Title = "ListView With Section"	
	
	Activity.AddMenuItem("Add", "addItem")	
	Activity.AddMenuItem("Reset", "reset")
	Activity.AddMenuItem("Refresh", "refresh")
	Activity.AddMenuItem("LogMapOne", "logmapone")
	Activity.AddMenuItem("LogMapHeader", "logmapheader")
	Activity.AddMenuItem("LogMapLine", "logmapline")
	Activity.AddMenuItem("LogMapShow", "logmapshow")
	Activity.AddMenuItem("resetbitmap", "resetbitmap")
	Activity.AddMenuItem("back", "back")
	
	objMapOfList.Initialize(Me, "EventHandler")	
	Dialog.Initialize(Activity)
	timer.Initialize("timer", 100)
	If bmHeader.IsInitialized = False Then
		bmHeader = getBitmap("H")
	End If
	If bmFooter.IsInitialized = False Then
		bmFooter = getBitmap("F")
	End If
	If bmCollapsed.IsInitialized = False Then
		bmCollapsed = getBitmap("C")
	End If
End Sub

Sub Activity_Resume
'	If clv1.IsInitialized Then
'		clv1.clear
'	End If
'	If objMapOfList.IsInitialized And objMapOfList.Size = 0 Then
'		objMapOfList.FillTheMap2
'	End If
	reset_click
End Sub

Sub Activity_Pause (UserClosed As Boolean)
	If timer.isInitialized Then
		timer.Enabled = False
	End If
End Sub

Private Sub resetbitmap_click
	bmHeader = getBitmap("H")
	bmFooter = getBitmap("F")
End Sub

Private Sub logmapone_click
	objMapOfList.LogStructure("mapone", objMapOfList.mapOne)
End Sub

Private Sub logmapheader_click
	objMapOfList.LogStructure("mapheader", objMapOfList.mapHeader)
End Sub

Private Sub logmapline_click
	objMapOfList.LogStructure("mapline", objMapOfList.mapLine)
End Sub

Private Sub logmapshow_click
	objMapOfList.LogStructure("mapshow", objMapOfList.mapShow)
End Sub

Private Sub reset_click
	If clv1.IsInitialized = False Then
		Return		
	End If
	If objMapOfList.IsInitialized = False Then
		Return
	End If
	clv1.Clear
	objMapOfList.ClearAll
	objMapOfList.FillTheMap2
End Sub

Private Sub refresh_click
	If clv1.IsInitialized = False Then
		Return 
	End If
	If objMapOfList.IsInitialized = False Then
		Return
	End If
	clv1.Clear
	FillListView
	clv1.Refresh
End Sub

Private Sub back_click
	Activity.Finish
End Sub

Private Sub getBitmap(whichtype As String) As B4XBitmap
	Dim lstType As List : lstType.Initialize2(Array As String("H", "h", "F", "f", "C", "c"))
	If lstType.IndexOf(whichtype) = -1 Then
		Return Null
	End If
	If whichtype.ToUpperCase = "H" Then		
		Return LoadBitmap(File.DirAssets, "sectionheader.png")
	End If
	If whichtype.ToUpperCase = "F" Then		
		Return LoadBitmap(File.DirAssets, "sectionfooter.png")		
	End If
	If whichtype.ToUpperCase = "C" Then
		Return LoadBitmap(File.DirAssets, "collapsed.png")
	End If
	Return Null
End Sub

Private Sub addItem_click
	
	If objMapOfList.IsInitialized = False Then
		Return
	End If	
	Dim p As B4XView = xui.CreatePanel("")
	p.SetLayoutAnimated(0, 0, 0, 250dip, 300dip) 'set the content size
	p.LoadLayout("InputBox.bal")	
	sprHead.DropdownBackgroundColor = Colors.White
	sprHead.DropdownTextColor = Colors.Black
	sprHead.TextColor = Colors.Black
	edtHead.Visible = False
	Dim lstChoice As List = objMapOfList.HeaderList
	lstChoice.InsertAt(0, "choose...")
	lstChoice.Add("new head...")
	sprHead.AddAll(lstChoice)
	sprHead.SelectedIndex = 0
	Dim rs As ResumableSub = Dialog.ShowCustom(p, "Ok", "", "Cancel")
	Wait For (rs) Complete (Result As Int)
	If Result = xui.DialogResponse_Positive Then		
		If sprHead.SelectedIndex = 0 Then 
			'choose...
			Return
		End If				
		If edtItem.Text = "" Or IsNumber(edtQty.Text) = False Then
			Return
		End If
		If sprHead.SelectedIndex = sprHead.Size -1 And edtHead.Text <> "" Then
			'add new head
			objMapOfList.AddItem( _
				CreateMap( _
					"head": CreateMap("text": edtHead.Text), _
					"line": CreateMap("item": edtItem.Text, "qty": edtQty.Text) _
				) _
			)
		End If
		If sprHead.SelectedIndex <> sprHead.Size -1 Then
			'add to existing head
			objMapOfList.AddItem( _
				CreateMap( _
					"head": CreateMap("text": sprHead.SelectedItem), _
					"line": CreateMap("item": edtItem.Text, "qty": edtQty.Text) _
				) _
			)
		End If
	End If
End Sub

Private Sub editHead(idx As Int, headid As Int)
	If objMapOfList.IsInitialized = False Then
		Return
	End If
	Dim text As String = objMapOfList.getTextByHeadId(headid)
	Dim p As B4XView = xui.CreatePanel("")
	p.SetLayoutAnimated(0, 0, 0, 250dip, 90dip) 'set the content size
	p.LoadLayout("EditHead.bal")	
	edtOHead.Visible = False
	edtOHead.Text = text
	edtHead.Visible = True
	edtHead.Text = text
	timer.Enabled = True
	Wait For timer_Tick
	timer.Enabled = False
	edtHead.RequestFocus
	edtHead.SelectAll
	Dim rs As ResumableSub = Dialog.ShowCustom(p, "Ok", "", "Cancel")
	Wait For (rs) Complete (Result As Int)
	If Result = xui.DialogResponse_Positive Then
		If edtOHead.Text = edtHead.Text Then
			' No Change
			Return		
		End If	
		If edtHead.Text = "" Then
			Return	
		End If
		objMapOfList.setUIIndex(idx)
		objMapOfList.EditHead( _
			CreateMap( _
				"head": CreateMap("headid": headid, "otext": edtOHead.Text, "text": edtHead.Text) _
			) _
		)		
	End If
End Sub

Private Sub editLine(idx As Int, headid As Int, lineid As Int)
	If objMapOfList.IsInitialized = False Then
		Return
	End If
	Dim text_1 As String = objMapOfList.getTextByHeadId(headid)	
	Dim item_1 As String = objMapOfList.getItemByLineId(lineid)
	Dim qty_1 As Int = objMapOfList.getQtyByLineId(lineid)
	Dim p As B4XView = xui.CreatePanel("")
	p.SetLayoutAnimated(0, 0, 0, 250dip, 220dip) 'set the content size
	p.LoadLayout("EditLine.bal")
	'edtHead.Text = text_1
	'edtHead.Enabled = False
	lblHead.Text = text_1
	edtOItem.Text = item_1
	edtItem.Text = item_1
	edtOQty.Text = qty_1
	edtQty.Text = qty_1
	timer.Enabled = True
	Wait For timer_Tick
	timer.Enabled = False
	edtItem.RequestFocus
	edtItem.SelectAll
	Dim rs As ResumableSub = Dialog.ShowCustom(p, "Ok", "", "Cancel")
	Wait For (rs) Complete (Result As Int)
	If Result = xui.DialogResponse_Positive Then
		If edtOItem.Text = edtItem.Text And edtOQty.Text = edtQty.Text Then
			'No Change
			Return
		End If
		If edtItem.Text = "" Or IsNumber(edtQty.Text) = False Then
			Return
		End If
		objMapOfList.setUIIndex(idx)
		objMapOfList.EditLine( _
			CreateMap( _
				"line": CreateMap("headid": headid, "lineid": lineid, "oitem": edtOItem.Text, "item": edtItem.Text, _
					"oqty": edtOQty.Text, "qty": edtQty.Text) _
			) _
		)
	End If
End Sub

Private Sub exchangeLine(idx As Int, headid As Int, lineid As Int)
	If objMapOfList.IsInitialized = False Then
		Return
	End If
	Dim text_1 As String = objMapOfList.getTextByHeadId(headid)
	Dim p As B4XView = xui.CreatePanel("")
	p.SetLayoutAnimated(0, 0, 0, 250dip, 300dip) 'set the content size
	p.LoadLayout("ExchangeLine.bal")
	sprHead.DropdownBackgroundColor = Colors.White
	sprHead.DropdownTextColor = Colors.Black
	sprHead.TextColor = Colors.Black
	edtHead.Visible = False
	Dim lstChoice As List = objMapOfList.HeaderList
	Dim foundidx As Int = lstChoice.IndexOf(text_1)
	If foundidx > -1 Then
		lstChoice.RemoveAt(foundidx)
	End If
	lstChoice.InsertAt(0, "choose...")
	lstChoice.Add("new head...")
	sprHead.AddAll(lstChoice)
	sprHead.SelectedIndex = 0
	lblHead.Text = text_1
	Dim rs As ResumableSub = Dialog.ShowCustom(p, "Ok", "", "Cancel")
	Wait For (rs) Complete (Result As Int)
	If Result = xui.DialogResponse_Positive Then
		If sprHead.SelectedIndex = 0 Then
			Return
		End If
		If sprHead.SelectedIndex = sprHead.Size -1 And edtHead.Text = "" Then
			Return
		End If
		If sprHead.SelectedIndex = sprHead.Size -1 Then
			objMapOfList.exchangeLine(headid, edtHead.Text, lineid, idx)
		Else
			objMapOfList.exchangeLine(headid, sprHead.SelectedItem, lineid, idx)
		End If		
	End If
End Sub

Private Sub EventHandler(mapRes As Map) 'ignore
	Dim action As String = mapRes.Get("action")	
	Dim uiindex As Int = -1
	Dim count As Int = -1
	Dim headid As Int = -1
	Dim lineid As Int = -1
	If mapRes.ContainsKey("uiindex") Then
		uiindex = mapRes.Get("uiindex")
	End If
	If mapRes.ContainsKey("count") Then
		count = mapRes.Get("count")
	End If
	If mapRes.ContainsKey("headid") Then
		headid = mapRes.Get("headid")
	End If
	If mapRes.ContainsKey("lineid") Then
		lineid = mapRes.Get("lineid")
	End If
	Try
		Select action
			Case "filled"
				FillListView
			Case "headdeleted"
				' First, delete the footer
				clv1.RemoveAt(uiindex + count + 1)				
				' Second, delete all children
				Dim j As Int = 0
				For j = count -1 To 0 Step -1
					Dim temp As String = clv1.GetValue(uiindex + j + 1)
					LogColor(temp, Colors.Blue)
					clv1.RemoveAt(uiindex + j + 1)
				Next
				' Third, delete the header
				LogIfIndexOutOfBound(311, uiindex)
				clv1.RemoveAt(uiindex)
				clv1.Refresh
			Case "linedeleted"
				LogColor(clv1.GetValue(uiindex), Colors.Blue)
				LogIfIndexOutOfBound(316, uiindex)
				clv1.RemoveAt(uiindex)
				clv1.Refresh
			Case "headadded"
				If headid = -1 Or lineid = -1 Then
					Return
				End If
				Dim pnlHead As B4XView = xui.CreatePanel("")
				pnlHead.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 35dip)
				'pnlHead.SetColorAndBorder(Colors.DarkGray, 3dip, Colors.Yellow, 20dip)
				'pnlHead.SetBitmap(bmHeader)
				pnlHead.Tag = "Header"
				clv1.Add(pnlHead, "H_" & headid)
				Dim pnlLine As B4XView = xui.CreatePanel("")
				pnlLine.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 40dip)
				pnlLine.Tag = "Line"
				clv1.Add(pnlLine, $"L_${headid}_${lineid}"$)				
				Dim pnlFoot As B4XView = xui.CreatePanel("")
				pnlFoot.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 35dip)
				'pnlFoot.SetColorAndBorder(Colors.DarkGray, 3dip, Colors.Green, 20dip)
				pnlFoot.SetBitmap(bmFooter)
				pnlFoot.Tag = "Footer"
				clv1.Add(pnlFoot, "F_" & headid)				
				timer.Enabled = True
				Wait For timer_tick
				timer.Enabled = False
				clv1.JumpToItem(clv1.Size-1)
			Case "lineadded"
				If headid = -1 Or lineid = -1 Then
					Return
				End If
				Dim pnlLine_1 As B4XView = xui.CreatePanel("")
				pnlLine_1.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 40dip)
				pnlLine_1.Tag = "Line"
				Dim nextheadid As Int = FindNextHeadIndex(headid)
				If nextheadid = -1 Or nextheadid = clv1.Size Then
					' No next head
					clv1.Add(pnlLine_1, $"L_${headid}_${lineid}"$)
					timer.Enabled = True
					Wait For timer_tick
					timer.Enabled = False
					clv1.JumpToItem(clv1.Size-1)
				Else
					LogIfIndexOutOfBound(357, nextheadid)
					clv1.InsertAt(nextheadid, pnlLine_1, $"L_${headid}_${lineid}"$)
					timer.Enabled = True
					Wait For timer_tick
					timer.Enabled = False
					If nextheadid = clv1.Size Then
						nextheadid = clv1.Size -1
					End If
					clv1.JumpToItem(nextheadid)
				End If
				clv1.Refresh
			Case "exchanged"
				'Dim pnltmp As B4XView = clv1.GetPanel(uiindex)
				ExchangedHandler(mapRes)
			Case "headedited"
				Dim pnlHead As B4XView = xui.CreatePanel("")
				pnlHead.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 35dip)
				'pnlHead.SetColorAndBorder(Colors.DarkGray, 3dip, Colors.Yellow, 20dip)
				'pnlHead.SetBitmap(bmHeader)
				pnlHead.Tag = "Header"
				clv1.ReplaceAt(uiindex, pnlHead, 35dip, "H_" & headid)
				clv1.Refresh
			Case "lineedited"
				Dim pnlLine_2 As B4XView = xui.CreatePanel("")
				pnlLine_2.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 40dip)
				pnlLine_2.Tag = "Line"
				clv1.ReplaceAt(uiindex, pnlLine_2, 40dip, $"L_${headid}_${lineid}"$)
				clv1.Refresh
			Case "collapsed"
				' hide the foot
				clv1.ResizeItem(uiindex + count + 1, 0dip)
				
				' hide lines
				Dim i As Int = 0
				For i = 0 To count -1
					clv1.ResizeItem(uiindex+i+1, 0dip)
				Next				
				'refresh the head
				Dim pnlHead_1 As B4XView = xui.CreatePanel("")
				pnlHead_1.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 35dip)
				'pnlHead_1.SetColorAndBorder(Colors.DarkGray, 3dip, Colors.Yellow, 20dip)
				'pnlHead_1.SetBitmap(bmHeader)
				pnlHead_1.Tag = "Header"
				clv1.ReplaceAt(uiindex, pnlHead_1, 35dip, "H_" & headid)
				clv1.Refresh
			Case "expanded"
				' show the foot
				clv1.ResizeItem(uiindex + count + 1, 35dip)
				' show lines
				Dim j As Int = 0
				For j = 0 To count -1
					clv1.ResizeItem(uiindex + j + 1, 40dip)
				Next				
				'refresh the head
				Dim pnlHead_2 As B4XView = xui.CreatePanel("")
				pnlHead_2.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 35dip)
				'pnlHead_2.SetColorAndBorder(Colors.DarkGray, 3dip, Colors.Yellow, 20dip)
				'pnlHead_2.SetBitmap(bmHeader)
				pnlHead_2.Tag = "Header"
				clv1.ReplaceAt(uiindex, pnlHead_2, 35dip, "H_" & headid)
				clv1.Refresh
		End Select
	Catch
		LogColor("EventHandler (" & action & "): " & CRLF & LastException, Colors.Red)
	End Try
	
End Sub

Private Sub ExchangedHandler(i_mapRes As Map)
	Dim headid As Int = -1
	If i_mapRes.ContainsKey("headid") Then
		headid = i_mapRes.Get("headid")
	End If
	Dim uiindex As Int = -1
	If i_mapRes.ContainsKey("uiindex") Then
		uiindex = i_mapRes.Get("uiindex")
	End If
	Try
		Select i_mapRes.Get("status")
			Case 0 ' Transfer in, Transfer out
				Dim pnlLine As B4XView = xui.CreatePanel("")
				pnlLine.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 40dip)
				pnlLine.Tag = "Line"
				'the lineid of value is kept and transfered
				Dim originalLineid As Int = UiindexToLineid(uiindex)
				If originalLineid = -1 Then
					Return
				End If
				Dim value As String = $"L_${headid}_${originalLineid}"$
				Dim nextheadid As Int = FindNextHeadIndex(headid)
				If nextheadid = -1 Or nextheadid = clv1.Size Then
					'No next head
					clv1.Add(pnlLine, value)
					LogIfIndexOutOfBound(446, uiindex)
					clv1.RemoveAt(uiindex)
				Else			
					If nextheadid < uiindex Then ' The larger index do first
						LogIfIndexOutOfBound(450, uiindex)
						clv1.RemoveAt(uiindex)
						LogIfIndexOutOfBound(452, nextheadid)
						clv1.InsertAt(nextheadid, pnlLine, value)
					Else
						LogIfIndexOutOfBound(455, nextheadid)						
						clv1.InsertAt(nextheadid, pnlLine, value)						
						LogIfIndexOutOfBound(457, uiindex)
						clv1.RemoveAt(uiindex)
					End If					
				End If				
				clv1.Refresh
				timer.Enabled = True
				Wait For timer_tick
				timer.Enabled = False
				If nextheadid = -1 Or nextheadid = clv1.Size Then
					nextheadid = clv1.Size - 1
				End If
				clv1.JumpToItem(nextheadid)
			Case 1 ' Transfer in, delete
				Dim pnlLine_1 As B4XView = xui.CreatePanel("")
				pnlLine_1.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 40dip)
				pnlLine_1.Tag = "Line"
				'the lineid of value is kept and transfered
				Dim originalLineid_1 As Int = UiindexToLineid(uiindex)
				If originalLineid_1 = -1 Then
					Return
				End If
				Dim value_1 As String =  $"L_${headid}_${originalLineid_1}"$
				Dim nextheadid_1 As Int = FindNextHeadIndex(headid)
				If nextheadid_1 = -1 Or nextheadid_1 = clv1.Size Then
					'No next head
					' Must add before delete
					clv1.Add(pnlLine_1, value_1)
					LogIfIndexOutOfBound(484, uiindex)
					clv1.RemoveAt(uiindex) 	 ' remove line
					LogIfIndexOutOfBound(486, uiindex-1)
					clv1.RemoveAt(uiindex-1) ' remove head
				Else
					If nextheadid_1 < uiindex Then ' The larger index do first
						LogIfIndexOutOfBound(490, uiindex+1)
						clv1.RemoveAt(uiindex+1) ' remove foot
						LogIfIndexOutOfBound(492, uiindex)
						clv1.RemoveAt(uiindex) 	 ' remove line
						LogIfIndexOutOfBound(494, uiindex-1)
						clv1.RemoveAt(uiindex-1) ' remove head
						LogIfIndexOutOfBound(496, nextheadid_1)
						clv1.InsertAt(nextheadid_1, pnlLine_1, value_1)
					Else
						LogIfIndexOutOfBound(499, nextheadid_1)
						clv1.InsertAt(nextheadid_1, pnlLine_1, value_1)
						LogIfIndexOutOfBound(501, uiindex+1)
						clv1.RemoveAt(uiindex+1) ' remove foot
						LogIfIndexOutOfBound(503, uiindex)
						clv1.RemoveAt(uiindex) 	 ' remove line
						LogIfIndexOutOfBound(505, uiindex-1)
						clv1.RemoveAt(uiindex-1) ' remove head
					End If
				End If				
				clv1.Refresh
				timer.Enabled = True
				Wait For timer_tick
				timer.Enabled = False
				If nextheadid_1 = -1 Or nextheadid_1 >= clv1.Size Then
					nextheadid_1 = clv1.Size -1
				End If
				clv1.JumpToItem(nextheadid_1)
			Case 2 ' insert, transfer out
				Dim pnlHead_2 As B4XView = xui.CreatePanel("")
				pnlHead_2.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 35dip)
				'pnlHead_2.SetColorAndBorder(Colors.DarkGray, 3dip, Colors.Yellow, 20dip)
				'pnlHead_2.SetBitmap(bmHeader)
				pnlHead_2.Tag = "Header"
				'the lineid of value is kept and transfered
				Dim originalLineid_2 As Int = UiindexToLineid(uiindex)
				If originalLineid_2 = -1 Then
					Return
				End If
				' Must add before delete
				Dim value_2 As String = $"L_${headid}_${originalLineid_2}"$
				clv1.Add(pnlHead_2, "H_" & headid)
				Dim pnlLine_2 As B4XView = xui.CreatePanel("")
				pnlLine_2.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 40dip)
				pnlLine_2.Tag = "Line"				
				clv1.Add(pnlLine_2, value_2)				
				Dim pnlFoot_2 As B4XView = xui.CreatePanel("")
				pnlFoot_2.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 35dip)
				'pnlFoot_2.SetColorAndBorder(Colors.DarkGray, 3dip, Colors.Green, 20dip)
				pnlFoot_2.SetBitmap(bmFooter)
				pnlFoot_2.Tag = "Footer"
				clv1.Add(pnlFoot_2, "F_" & headid)				
				
				LogIfIndexOutOfBound(540, uiindex)
				clv1.RemoveAt(uiindex) 	 ' remove line				
				
				timer.Enabled = True
				Wait For timer_tick
				timer.Enabled = False
				clv1.JumpToItem(clv1.Size-1)
			Case 3 ' insert, delete
				Dim pnlHead_3 As B4XView = xui.CreatePanel("")
				pnlHead_3.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 35dip)
				'pnlHead_3.SetColorAndBorder(Colors.DarkGray, 3dip, Colors.Yellow, 20dip)
				'pnlHead_3.SetBitmap(bmHeader)
				pnlHead_3.Tag = "Header"
				'the lineid of value is kept and transfered
				Dim originalLineid_3 As Int = UiindexToLineid(uiindex)
				If originalLineid_3 = -1 Then
					Return
				End If
				' Must add before delete
				Dim value_3 As String = $"L_${headid}_${originalLineid_3}"$
				clv1.Add(pnlHead_3, "H_" & headid)
				Dim pnlLine_3 As B4XView = xui.CreatePanel("")
				pnlLine_3.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 40dip)
				pnlLine_3.Tag = "Line"				
				clv1.Add(pnlLine_3, value_3)				
				Dim pnlFoot_3 As B4XView = xui.CreatePanel("")
				pnlFoot_3.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 35dip)
				'pnlFoot_3.SetColorAndBorder(Colors.DarkGray, 3dip, Colors.Green, 20dip)
				pnlFoot_3.SetBitmap(bmFooter)
				pnlFoot_3.Tag = "Footer"
				clv1.Add(pnlFoot_3, "F_" & headid)				
				LogIfIndexOutOfBound(569, uiindex+1)
				clv1.RemoveAt(uiindex+1) ' remove foot
				LogIfIndexOutOfBound(571, uiindex)
				clv1.RemoveAt(uiindex) 	 ' remove line
				LogIfIndexOutOfBound(573, uiindex-1)
				clv1.RemoveAt(uiindex-1) ' remove head
				
				timer.Enabled = True
				Wait For timer_tick
				timer.Enabled = False
				clv1.JumpToItem(clv1.Size-1)
			Case Else
					
		End Select
	Catch
		Dim errmsg As String = "ExchangedHandler: " & LastException.Message
		LogColor(errmsg, Colors.red)
		ToastMessageShow(errmsg, True)
	End Try	
End Sub

Private Sub UiindexToLineid(uiindex As Int) As Int
	Dim value As String = clv1.GetValue(uiindex)
	Dim arr() As String = Regex.Split("\_", value)
	If arr.Length <> 3 Then
		Return -1
	End If
	Return arr(2)
End Sub

Private Sub FindNextHeadIndex(headid As Int) As Int
	Dim idx_1 As Int = -1
	Dim i As Int = 0
	For i = 0 To clv1.Size-1
		If clv1.GetValue(i) = "H_" & headid Then
			idx_1 = i
			Exit
		End If
	Next
	If idx_1 = -1 Then
		Return -1
	End If
	Dim idx_2 As Int = -1
	Dim j As Int = 0
	For j = idx_1 + 1 To clv1.Size -1
		Dim tempvalue As String = clv1.GetValue(j)
		If tempvalue.SubString2(0, 1) = "F" Then
			idx_2 = j
			If idx_2 >= clv1.Size Then
				LogColor("headid: " & headid, Colors.Red)
			End If
			Exit
		End If
	Next
	Return idx_2
End Sub

Private Sub clv1_ItemClick (Index As Int, Value As Object)	
	Log("Value: " & Value)
	If objMapOfList.IsInitialized = False Then
		Return
	End If
	Dim arrVal() As String = Regex.Split("\_", Value)
	
	Select arrVal(0)
		Case "H"
			Dim head_id As Int = arrVal(1)
			If objMapOfList.isHeadExpanded(head_id) Then
				objMapOfList.collapseHead(head_id, Index)
			Else
				objMapOfList.expandHead(head_id, Index)
			End If
		Case "F"
		Case "L"
			Dim str As String = objMapOfList.getContent(Value)
			Dim arr() As String = Regex.Split("\^", str)
			If arr.Length = 2 Then
				Dim msg1 As String = $"Index: ${Index}${CRLF}Value: ${Value}${CRLF}Item: ${arrVal(0)}${CRLF}Qty: ${arr(1)}"$
				Log("ItemClick (Line) - " & CRLF & msg1.Replace(CRLF, "; "))
				Msgbox2Async(msg1, "Line Info", "OK", "", "", Null, True)
			End If			
	End Select
	
	
'	Dim str As String = objMapOfList.getContent(Value)
'	
'	If str.IndexOf("^") = -1 Then
''		Dim msg As String = $"Index: ${Index}${CRLF}Value: ${Value}${CRLF}Text: ${str}"$ 
''		Log("ItemClick (Head) - " & CRLF & msg.Replace(CRLF, "; "))
''		Msgbox2Async(msg, "Head Info", "OK", "", "", Null, True)
'		Dim arrH() As String = Regex.Split("\_", Value)
'		Dim head_id As Int = arrH(1)
'		If objMapOfList.isHeadExpanded(head_id) Then
'			objMapOfList.collapseHead(head_id, Index)
'			'clv1.refresh
'		Else
'			objMapOfList.expandHead(head_id, Index)
'			'clv1.Refresh
'		End If
'		Return
'	End If
'	Dim arr() As String = Regex.Split("\^", str)
'	If arr.Length = 2 Then
'		Dim msg1 As String = $"Index: ${Index}${CRLF}Value: ${Value}${CRLF}Item: ${arr(0)}${CRLF}Qty: ${arr(1)}"$
'		Log("ItemClick (Line) - " & CRLF & msg1.Replace(CRLF, "; "))
'		Msgbox2Async(msg1, "Line Info", "OK", "", "", Null, True)
'		Return
'	End If
	'Msgbox2Async(
End Sub

Private Sub clv1_ItemLongClick(Index As Int, Value As Object)
	'Type conversion
	Dim strTemp As String = Value	
	Dim arrTemp() As String = Regex.Split("\_", strTemp)
	Dim tagtype As String = arrTemp(0)
	If tagtype <> "H" And tagtype <> "L" Then
		Return 
	End If
	'Type conversion
	Dim head_id As Int = arrTemp(1)
	Dim lstMenu As List
	If tagtype = "H" Then ' e.g. H_1		
		lstMenu.Initialize2(Array As String("headinfo", "edit", "delete", "cancel"))
	End If
	If tagtype = "L" Then ' e.g. L_2_3
		lstMenu.Initialize2(Array As String("edit", "delete", "exchange", "cancel"))
	End If
	
	Select modCommon.popupMenu2(lstMenu)
		Case "headinfo"
			Dim str As String = objMapOfList.getContent(Value)
			Dim msg As String = $"Index: ${Index}${CRLF}Value: ${Value}${CRLF}Text: ${str}"$
			Log("ItemClick (Head) - " & CRLF & msg.Replace(CRLF, "; "))
			Msgbox2Async(msg, "Head Info", "OK", "", "", Null, True)
		Case "edit"
			If tagtype = "H" Then
				editHead(Index, arrTemp(1))
				Return
			End If
			If tagtype = "L" Then
				editLine(Index, arrTemp(1), arrTemp(2))
				Return				
			End If			
		Case "delete"			
			If tagtype = "H" Then
				objMapOfList.setUIIndex(Index)
				'arrTemp(1) is header id
				objMapOfList.DeleteHeader(arrTemp(1))				
				Return
			End If
			If tagtype = "L" Then
				objMapOfList.setUIIndex(Index)
				'arrTemp(1) is item's header id
				'arrTemp(2) is item id
				objMapOfList.DeleteItem(arrTemp(1), arrTemp(2))				
				Return
			End If
		Case "exchange"
			exchangeLine(Index, head_id, arrTemp(2))
		Case "cancel"
			Return
		Case Else
			Return
	End Select
End Sub

Private Sub clv1_VisibleRangeChanged (FirstIndex As Int, LastIndex As Int)
	Dim ExtraSize As Int = 18 'Extra Size MUST cover the whole screen + extra 2 rows in case of head add or exchange
	
	For i = 0 To clv1.Size - 1
		Dim pnl_1 As B4XView = clv1.GetPanel(i)
		If i > FirstIndex - ExtraSize And i < LastIndex + ExtraSize Then
			'Visible +
			If pnl_1.NumberOfViews = 0 Then
				Dim value() As String = Regex.Split("\_", clv1.GetValue(i))	
				Dim valOne As Int = value(1)			
				Select value(0)
					Case "H"
						If objMapOfList.mapHeader.ContainsKey(valOne) Then
							Dim mapEntry1 As Map = objMapOfList.mapHeader.Get(valOne)
							Dim myTitle As String = mapEntry1.Get("text")
							Dim isExpanded As Boolean = False
							If objMapOfList.IsInitialized Then
								isExpanded = objMapOfList.isHeadExpanded(valOne)
							End If
							EquipHeaderPanel(pnl_1, "HEADER #" & valOne & " " & myTitle, isExpanded)
							If isExpanded Then
								' Refresh the Footer when expanded
								Dim itsfootid As Int = FindNextHeadIndex(valOne)
								Dim pnl_footer As B4XView = clv1.GetPanel(itsfootid)
								EquipFooterPanel(pnl_footer, "FOOTER #" & valOne)
							End If
						End If						
					Case "L"						
						Dim valTwo As Int = value(2)
						If objMapOfList.mapLine.ContainsKey(valTwo) Then
							Dim mapEntry2 As Map = objMapOfList.mapLine.Get(valTwo)
							Dim myQty As Int = mapEntry2.Get("qty")
							Dim myItem As String = mapEntry2.Get("item")
							EquipItemPanel(pnl_1, "LINE #" & valTwo & " " & myItem, myQty, 40dip)
						End If		
					Case "F"
						If objMapOfList.mapHeader.ContainsKey(valOne) Then
							'Dim mapEntry3 As Map = objMapOfList.mapHeader.Get(valOne)
							'Dim myTitle As String = mapEntry3.Get("text")							
							EquipFooterPanel(pnl_1, "FOOTER #" & valOne)
						End If
					Case Else
						Continue
				End Select				
			End If
		End If
	Next
End Sub

Sub FillListView()
	If objMapOfList.IsInitialized = False Then
		Return
	End If
	For Each key_1 As Int In objMapOfList.mapOne.Keys
		Dim lstTmp As List = objMapOfList.mapOne.Get(key_1)
		If lstTmp.IsInitialized = False Then
			Continue
		End If
		Dim pnlHead As B4XView = xui.CreatePanel("")
		pnlHead.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 35dip)
		'pnlHead.SetColorAndBorder(Colors.DarkGray, 3dip, Colors.Yellow, 20dip)
		'pnlHead.SetBitmap(bmHeader)
		pnlHead.Tag = "Header"
		clv1.Add(pnlHead, "H_" & key_1)
		For Each entry_1 As Int In lstTmp
			Dim pnlLine As B4XView = xui.CreatePanel("")
			pnlLine.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 40dip)
			pnlLine.Tag = "Line"			
			clv1.Add(pnlLine, $"L_${key_1}_${entry_1}"$)
		Next
		Dim pnlFoot As B4XView = xui.CreatePanel("")
		pnlFoot.SetLayoutAnimated(0, 0, 0, clv1.AsView.Width, 35dip)
		'pnlFoot.SetColorAndBorder(Colors.DarkGray, 3dip, Colors.Green, 20dip)
		'pnlFoot.SetBitmap(bmFooter)
		pnlFoot.Tag = "Footer"
		clv1.Add(pnlFoot, "F_" & key_1)
	Next
End Sub

' Pass panel as Reference
Sub EquipItemPanel(pItem As Panel, Text As String, Text2 As String, Height As Int) 
	
	Dim lbl As Label
	lbl.Initialize("")
	lbl.Gravity = Bit.Or(Gravity.CENTER_VERTICAL, Gravity.LEFT)
	lbl.Text = Text
	lbl.TextSize = 16
	lbl.TextColor = Colors.Black	
	Dim lbl2 As Label
	lbl2.Initialize("")
	lbl2.Gravity = Bit.Or(Gravity.CENTER_VERTICAL, Gravity.LEFT)
	lbl2.TextSize = 16
	lbl2.TextColor = Colors.Black
	lbl2.Text = Text2	
	pItem.AddView(lbl, 5dip, 2dip, 150dip, Height - 4dip) 'view #0
	pItem.AddView(lbl2, 280dip, 2dip, 50dip, Height - 4dip) 'view #2
End Sub
' Pass panel as Reference
Sub EquipHeaderPanel(pHeader As B4XView, Text As String, isExpanded As Boolean) 
	
	pHeader.Color = Colors.DarkGray

	Dim lbl As Label : lbl.Initialize("")
	lbl.Gravity = Bit.Or(Gravity.CENTER_VERTICAL, Gravity.CENTER_HORIZONTAL)
	lbl.Text = Text
	lbl.TextSize = 16
	lbl.Typeface = Typeface.DEFAULT_BOLD
	lbl.TextColor = Colors.White	
	Dim lbl1 As Label : lbl1.Initialize("")
	lbl1.Gravity = Bit.Or(Gravity.CENTER_VERTICAL, Gravity.RIGHT)
	If isExpanded Then
		lbl1.Text = modCommon.UTS(0x1F53A) ' ^
		pHeader.SetBitmap(bmHeader)
	Else
		lbl1.Text = modCommon.UTS(0x1F53B) ' v
		pHeader.SetBitmap(bmCollapsed)
	End If
	pHeader.AddView(lbl, 5dip, 2dip, 320dip, 35dip)
	pHeader.AddView(lbl1, 280dip, 2dip, 20dip, 35dip)
End Sub
' Pass panel as Reference
Sub EquipFooterPanel(pFooter As B4XView, Text As String) 
	
	pFooter.Color = Colors.Gray
	pFooter.SetBitmap(bmFooter)
	Dim lbl As Label : lbl.Initialize("")
	lbl.Gravity = Bit.Or(Gravity.CENTER_VERTICAL, Gravity.CENTER_HORIZONTAL)
	lbl.Text = Text
	lbl.TextSize = 16
	lbl.Typeface = Typeface.DEFAULT_BOLD
	lbl.TextColor = Colors.White	
	Dim lbl1 As Label : lbl1.Initialize("")
	lbl1.Gravity = Bit.Or(Gravity.CENTER_VERTICAL, Gravity.RIGHT)
	pFooter.AddView(lbl, 5dip, 2dip, 320dip, 35dip)
	pFooter.AddView(lbl1, 280dip, 2dip, 20dip, 35dip)
End Sub

Private Sub sprHead_ItemClick (Position As Int, Value As Object)
	
	If Position = sprHead.Size - 1 Then 'new item
		edtHead.Visible = True
	Else
		edtHead.Visible = False		
	End If
	timer.Enabled = True
	Wait For timer_Tick
	timer.Enabled = False
	edtHead.Text = ""
	edtHead.RequestFocus
	edtHead.SelectAll	
End Sub

Private Sub LogIfIndexOutOfBound(whichline As Int, idx As Int)
	If clv1.IsInitialized = False Then
		Return
	End If
	If idx >= clv1.Size Then
		Dim err As String = "Which line: " & whichline & CRLF & _
			"Index: " & idx & CRLF & _
			"clv1 size: " & clv1.Size
		LogColor(err, Colors.Red)
		Msgbox2Async(err, "IndexOutOfBound", "OK", "", "", Null, True)
	End If
End Sub

Private Sub edtQty_FocusChanged (HasFocus As Boolean)
	If HasFocus Then
		timer.Enabled = True
		Wait For timer_tick
		timer.Enabled = False
		edtQty.SelectAll
	End If
End Sub